buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.bmuschko:gradle-cargo-plugin:2.6.1"
    }
}

plugins {
    id 'java'
    id "org.flywaydb.flyway" version "7.4.0"
}

sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
}

subprojects {

    group 'pl.tpolgrabia.trainings.javaee'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'com.bmuschko.cargo'

    sourceCompatibility = '1.8'

    dependencies {
        def cargoVersion = '1.6.5'
        cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
                "org.codehaus.cargo:cargo-ant:$cargoVersion"
    }

    cargo {
        containerId = 'wildfly10x'

        remote {
            hostname = project.ext["javaeeshowcase.cargo.wildfly.hostname"]
            username = project.ext["javaeeshowcase.cargo.wildfly.username"]
            password = project.ext["javaeeshowcase.cargo.wildfly.password"]

            containerProperties {
                property 'cargo.jboss.management-http.port',
                        project.ext["javaeeshowcase.cargo.wildfly.port"]
            }
        }
    }
}

flyway {
    url = project.ext['javaeeshowcase.flyway.url']
    user = project.ext['javaeeshowcase.flyway.username']
    password = project.ext['javaeeshowcase.flyway.password']
    schemas = project.ext['javaeeshowcase.flyway.schemas']
}
